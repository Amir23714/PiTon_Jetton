;; Jetton Wallet Smart Contract
#include "imports/stdlib.fc";
#include "jetton-utils.fc";
#include "op-codes.fc";
#include "params.fc";

int min_tons_for_storage() asm "10000000 PUSHINT"; ;; 0.01 TON
;; Note that 2 * gas_consumptions is expected to be able to cover fees on both wallets (sender and receiver)
;; and also constant fees on inter-wallet interaction, in particular fwd fee on state_init transfer
;; that means that you need to reconsider this fee when:
;; a) jetton logic become more gas-heavy
;; b) jetton-wallet code (sent with inter-wallet message) become larger or smaller
;; c) global fee changes / different workchain
int gas_consumption() asm "15000000 PUSHINT"; ;; 0.015 TON
const int const::notification_amount = 10000000;

{-
  Storage
  storage#_ balance:Coins owner_address:MsgAddressInt jetton_master_address:MsgAddressInt jetton_wallet_code:^Cell = Storage;
-}

(int, slice, slice, cell) load_data() inline {
    slice ds = get_data().begin_parse();
    return (ds~load_coins(), ;; Current Jetton balance
        ds~load_msg_addr(), ;; Address of the owner of the wallet
        ds~load_msg_addr(), ;; Address of the Jetton Master contract
        ds~load_ref() ;; Code of the Jetton Wallet contract
    );
}

() save_data (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) impure inline {
    set_data(pack_jetton_wallet_data(balance, owner_address, jetton_master_address, jetton_wallet_code));
}

{-
  transfer query_id:uint64 amount:(VarUInteger 16) destination:MsgAddress
           response_destination:MsgAddress custom_payload:(Maybe ^Cell)
           forward_ton_amount:(VarUInteger 16) forward_payload:(Either Cell ^Cell)
           = InternalMsgBody;
  internal_transfer  query_id:uint64 amount:(VarUInteger 16) from:MsgAddress
                     response_address:MsgAddress
                     forward_ton_amount:(VarUInteger 16)
                     forward_payload:(Either Cell ^Cell) 
                     = InternalMsgBody;
-}

() send_tokens (slice in_msg_body, slice sender_address, int msg_value, int fwd_fee) impure {

    int query_id = in_msg_body~load_uint(64);
    int jetton_amount = in_msg_body~load_coins();

    ;; Address of owner of the wallet to which the jetton is being sent
    slice to_owner_address = in_msg_body~load_msg_addr();

    ;; Throw exception if to_owner_address is not presented in the worckchain 0
    force_chain(to_owner_address);

    (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = load_data();
    
    ;; Decrease the balance of the sender with the amount of jetton being sent
    balance -= jetton_amount;

    ;; Throw exception if sender address is not equal to owner address
    throw_unless(705, equal_slices(owner_address, sender_address));
    ;; Throw exception if balance became less than 0
    throw_unless(706, balance >= 0);

    ;; Calculate jetton wallet address of the to_owner_address
    cell state_init = calculate_jetton_wallet_state_init(to_owner_address, jetton_master_address, jetton_wallet_code);
    slice to_wallet_address = calculate_jetton_wallet_address(state_init); ;; Address of the wallet to which the jettons is being sent

    ;; Address to be notificated about the transfer
    slice response_address = in_msg_body~load_msg_addr();
    cell custom_payload = in_msg_body~load_dict();
    int forward_ton_amount = in_msg_body~load_coins();

    ;; Check that forward_payload is not empty
    throw_unless(708, slice_bits(in_msg_body) >= 1);
    slice either_forward_payload = in_msg_body;

    ;; Construct the message to be sent to to_wallet_address
    var msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(to_wallet_address)
        .store_coins(0)
        .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
        .store_ref(state_init);

    ;; Construct message body with standard: https://github.com/ton-blockchain/TIPs/issues/74
    var msg_body = begin_cell()
        .store_uint(op::internal_transfer(), 32)
        .store_uint(query_id, 64)
        .store_coins(jetton_amount)
        .store_slice(owner_address) ;; address of the owner of current jetton wallet
        .store_slice(response_address)
        .store_coins(forward_ton_amount)
        .store_slice(either_forward_payload)
        .end_cell();
    msg = msg.store_ref(msg_body);
    
    int fwd_count = forward_ton_amount ? 2 : 1;

    ;; Throw exception if msg_value is not sufficient to cover the transfer and forward fees
    throw_unless(709, msg_value >
                     forward_ton_amount +
        ;; 3 messages: wal1->wal2,  wal2->owner, wal2->response
        ;; but last one is optional (it is ok if it fails)
                     fwd_count * fwd_fee +
        (2 * gas_consumption() + min_tons_for_storage()));
    ;; universal message send fee calculation    may be activated here
    ;; by using this instead of fwd_fee
    ;; msg_fwd_fee(to_wallet, msg_body, state_init, 15)

    send_raw_message(msg.end_cell(), 64); ;; revert on errors and Carry all the remaining value of the inbound message in addition to the value initially indicated in the new message
    save_data(balance, owner_address, jetton_master_address, jetton_wallet_code);
}

{-
  internal_transfer  query_id:uint64 amount:(VarUInteger 16) from:MsgAddress
                     response_address:MsgAddress
                     forward_ton_amount:(VarUInteger 16)
                     forward_payload:(Either Cell ^Cell) 
                     = InternalMsgBody;
-}

() receive_tokens (slice in_msg_body, slice sender_address, int my_ton_balance, int fwd_fee, int msg_value) impure {
    ;; NOTE we can not allow fails in action phase since in that case there will be
    ;; no bounce. Thus check and throw in computation phase.
    (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = load_data();

    int query_id = in_msg_body~load_uint(64);
    int jetton_amount = in_msg_body~load_coins();

    ;; Increase balance of the wallet with the amount of jetton being received
    balance += jetton_amount;

    int ton_balance_before_msg = my_ton_balance - msg_value;

    ;; If it is request from MasterContract.mint_tokens()
    if (equal_slices(jetton_master_address, sender_address)){

        ;; If current Balance covers storage fee, then we should not keep any TONs from received value
        int storage_fee = min_tons_for_storage() - min(ton_balance_before_msg, min_tons_for_storage());

        msg_value -= (storage_fee + gas_consumption());

        ;; Send notification to the owner of the wallet about successful minting of jettons
        msg_value -= (const::notification_amount + fwd_fee);

        slice either_forward_payload = in_msg_body;
        var msg_body = begin_cell()
            .store_uint(op::transfer_notification(), 32)
            .store_uint(query_id, 64)
            .store_coins(jetton_amount)
            .store_slice(jetton_master_address) ;; address of the master contract
            .store_slice(either_forward_payload)
            .end_cell();

        var msg = begin_cell()
            .store_uint(0x10, 6) ;; we should not bounce here cause receiver can have uninitialized contract
            .store_slice(owner_address)
            .store_coins(const::notification_amount)
            .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_ref(msg_body);

        ;; Send message about minting jettons to owner of the wallet
        send_raw_message(msg.end_cell(), 1); ;; fwd_fee covers cost of sending message

        ;; Send response message to the master contract with the rest of incoming TONs
        var msg = begin_cell()
            .store_uint(0x10, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 010000
            .store_slice(jetton_master_address)
            .store_coins(msg_value)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(op::excesses(), 32)
            .store_uint(query_id, 64);
        send_raw_message(msg.end_cell(), 2);

        save_data(balance, owner_address, jetton_master_address, jetton_wallet_code);
        return();
    }

    ;; If it is not MINT request, then it is TRANSFER request

    ;; Address of the owner of the jetton wallet contract from which the jettons are being sent
    slice from_address = in_msg_body~load_msg_addr();
    ;; Address to send the response with confirmation of successful transfer and the rest of incoming TONs
    slice response_address = in_msg_body~load_msg_addr();

    ;; Accept request if sender is jetton wallet of from_address contract
    throw_unless(707,
        equal_slices(calculate_user_jetton_wallet_address(from_address, jetton_master_address, jetton_wallet_code), sender_address)
    );

    ;; Amount of TONs to be forwarded to response_address
    int forward_ton_amount = in_msg_body~load_coins();

    ;; If current Balance covers storage fee, then we should not keep any TONs from received value
    int storage_fee = min_tons_for_storage() - min(ton_balance_before_msg, min_tons_for_storage());
    ;; Extract storage_fee(may be zero) and gas consumption (for executing contract code) from the received value
    msg_value -= (storage_fee + gas_consumption());

    if(forward_ton_amount) {
        msg_value -= (forward_ton_amount + fwd_fee);
        slice either_forward_payload = in_msg_body;

        var msg_body = begin_cell()
            .store_uint(op::transfer_notification(), 32)
            .store_uint(query_id, 64)
            .store_coins(jetton_amount)
            .store_slice(from_address) ;; address of the previous owner of transferred jettons
            .store_slice(either_forward_payload)
            .end_cell();

        var msg = begin_cell()
            .store_uint(0x10, 6) ;; we should not bounce here cause receiver can have uninitialized contract
            .store_slice(owner_address)
            .store_coins(forward_ton_amount)
            .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_ref(msg_body);

        send_raw_message(msg.end_cell(), 1);
    }

    if ((response_address.preload_uint(2) != 0) & (msg_value > 0)) {
        var msg = begin_cell()
            .store_uint(0x10, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 010000
            .store_slice(response_address)
            .store_coins(msg_value)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(op::excesses(), 32)
            .store_uint(query_id, 64);
        send_raw_message(msg.end_cell(), 2);
    }

    save_data(balance, owner_address, jetton_master_address, jetton_wallet_code);
}

() burn_tokens (slice in_msg_body, slice sender_address, int msg_value, int fwd_fee) impure {
    ;; NOTE we can not allow fails in action phase since in that case there will be
    ;; no bounce. Thus check and throw in computation phase.
    (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = load_data();
    
    int query_id = in_msg_body~load_uint(64);
    ;; amount of jettons to be burned
    int jetton_amount = in_msg_body~load_coins();
    slice response_address = in_msg_body~load_msg_addr();

    ;; ignore custom payload
    ;; slice custom_payload = in_msg_body~load_dict();

    ;; Decrease the balance of the wallet with the amount of jetton being burned
    balance -= jetton_amount;

    ;; Throw exception if sender address is not owner of jetton wallet
    throw_unless(705, equal_slices(owner_address, sender_address));
    ;; Throw exception if balance became less than 0
    throw_unless(706, balance >= 0);
    ;; no enough TONs to send to response_destination: at least (in_msg_value -  max_tx_gas_price)
    throw_unless(707, msg_value > fwd_fee + 2 * gas_consumption());

    var msg_body = begin_cell()
      .store_uint(op::burn_notification(), 32)
      .store_uint(query_id, 64)
      .store_coins(jetton_amount)
      .store_slice(owner_address)
      .store_slice(response_address)
      .end_cell();

    var msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(jetton_master_address)
        .store_coins(0)
        .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_ref(msg_body);

    ;; Send message about burning jettons to master contract
    send_raw_message(msg.end_cell(), 64);

    save_data(balance, owner_address, jetton_master_address, jetton_wallet_code);
}

() on_bounce (slice in_msg_body) impure {
    in_msg_body~skip_bits(32); ;; 0xFFFFFFFF
    (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = load_data();

    int op = in_msg_body~load_uint(32);
    throw_unless(709, (op == op::internal_transfer()) | (op == op::burn_notification()));

    int query_id = in_msg_body~load_uint(64);
    int jetton_amount = in_msg_body~load_coins();

    balance += jetton_amount;
    save_data(balance, owner_address, jetton_master_address, jetton_wallet_code);
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    ;; if message is bounced => on_bounce()
    if (flags & 1) {
        on_bounce(in_msg_body);
        return ();
    }

    slice sender_address = cs~load_msg_addr();
    cs~load_msg_addr(); ;; skip dst
    cs~load_coins(); ;; skip value
    cs~skip_bits(1); ;; skip extracurrency collection
    cs~load_coins(); ;; skip ihr_fee
    int fwd_fee = muldiv(cs~load_coins(), 3, 2); ;; we use message fwd_fee for estimation of forward_payload costs

    int op = in_msg_body~load_uint(32);

    if (op == op::transfer()) { ;; outgoing transfer
        send_tokens(in_msg_body, sender_address, msg_value, fwd_fee);
        return ();
    }

    if (op == op::internal_transfer()) { ;; incoming transfer
        receive_tokens(in_msg_body, sender_address, my_balance, fwd_fee, msg_value);
        return ();
    }

    if (op == op::burn()) { ;; burn
        burn_tokens(in_msg_body, sender_address, msg_value, fwd_fee);
        return ();
    }

    throw(0xffff);
}

(int, slice, slice, cell) get_wallet_data() method_id {
    return load_data();
}